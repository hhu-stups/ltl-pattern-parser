apply plugin: 'antlr'
apply plugin: 'java-library'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

project.group = 'de.hhu.stups'
project.version = '0.1.4-SNAPSHOT'

final isSnapshot = project.version.endsWith("-SNAPSHOT")

repositories {
	mavenCentral()
}

dependencies {
	antlr "org.antlr:antlr4:4.7.2" // use ANTLR version 4
	api "org.antlr:antlr4-runtime:4.7.2"
	api "de.hhu.stups:parserbase:2.4.40"
	api "de.hhu.stups:prologlib:2.4.40"

	testImplementation "de.hhu.stups:ltlparser:2.4.40"
	testImplementation 'junit:junit:4.8.2'
}

generateGrammarSource.outputs.upToDateWhen { false }

java {
	withSourcesJar()
	withJavadocJar()
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar

			pom {
				name = 'LTL Pattern DSL Parser'
				description = 'Parser for a LTL Pattern DSL'
				url = 'https://gitlab.cs.uni-duesseldorf.de/stups/prob/ltl-pattern-parser'

				licenses {
					license {
						name = 'Eclipse Public License, Version 1.0'
						url = 'https://www.eclipse.org/legal/epl-v10.html'
					}
				}
				scm {
					connection = 'scm:git:git://gitlab.cs.uni-duesseldorf.de/stups/prob/ltl-pattern-parser.git'
					developerConnection = 'scm:git:git@gitlab.cs.uni-duesseldorf.de/stups/prob/ltl-pattern-parser.git'
					url = 'https://gitlab.cs.uni-duesseldorf.de/stups/prob/ltl-pattern-parser'
				}


				developers {
					developer {
						id = 'bendisposto'
						name = 'Jens Bendisposto'
						email = 'jens@bendisposto.de'
					}
				}
			}
		}
	}

	repositories {
		maven {
			final releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
			final snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
			url isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
			if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
				credentials {
					username project.ossrhUsername
					password project.ossrhPassword
				}
			}
		}
	}
}

ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

signing {
	sign publishing.publications.mavenJava
}
